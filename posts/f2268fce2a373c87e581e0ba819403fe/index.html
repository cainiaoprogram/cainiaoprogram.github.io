<!DOCTYPE html>
<html class="no-js" lang="en">
<head>
	<meta charset="UTF-8">
	<meta name="viewport" content="width=device-width, initial-scale=1">
	<title>[图像处理]3×3中值滤波的verilog实现 - 菜鸟程序员博客</title>
	<script>(function(d,e){d[e]=d[e].replace("no-js","js");})(document.documentElement,"className");</script>
	<meta name="description" content="">
		<meta property="og:title" content="[图像处理]3×3中值滤波的verilog实现" />
<meta property="og:description" content="一、原理 1.将连续输入的9个灰度值存入移位寄存器，并通过抽头形成3×3矩阵，而后通过组合逻辑输出9个数的中值。 2.抽头原理 因为数据存入shift reg需要一拍的时间，若选用5、2抽头需要对input单独延迟一拍，本例选用6、3抽头以及input作为滤波矩阵的列输入，避免对input的单独延迟处理。
每个时钟上升沿到来时，矩阵左列为抽头输入，其他列依次右移，如图所示。
3.求中值算法 ①将9个数分成3组，每组3个数。求每一组的最大值、中值、最小值
②对3个最大值求最小值，对3个最小值求最大值，对3个中值求中值，得到3个数
③对这三个数求中值，这个数也是9个像素的中值
二、verilog实现 顶层模块：
// Module Name: mid_9_nums // Target Device: xc7z010clg400-1 // Tool versions: vivado 2017.4 // Last Version: 2022.4.18 // Description: mid_filter //port define module mid_9_nums( input clk, input rst_n, input [7:0] data_in, output [7:0] data_out ); parameter length = 9; reg [7:0] data_reg [length-1:0];//9 shift reg /*********max/mid/min reg**********/ wire [7:0] data_max [2:0]; wire [7:0] data_mid [2:0]; wire [7:0] data_min [2:0]; wire [7:0] data_min_max; wire [7:0] data_max_min; wire [7:0] data_mid_mid; /**********************************/ /**************matrix**************/ reg [7:0] m11,m12,m13; reg [7:0] m21,m22,m23; reg [7:0] m31,m32,m33; /**********************************/ wire [7:0] row1,row2; reg	[7:0] row3; integer i; //9 nums shift reg always @(posedge clk or negedge rst_n)begin if(!" />
<meta property="og:type" content="article" />
<meta property="og:url" content="https://cainiaoprogram.github.io/posts/f2268fce2a373c87e581e0ba819403fe/" /><meta property="article:section" content="posts" />
<meta property="article:published_time" content="2022-05-16T08:46:19+08:00" />
<meta property="article:modified_time" content="2022-05-16T08:46:19+08:00" />


	<link rel="preconnect" href="https://fonts.gstatic.com" crossorigin>
	<link rel="dns-prefetch" href="//fonts.googleapis.com">
	<link rel="dns-prefetch" href="//fonts.gstatic.com">
	<link rel="stylesheet" href="https://fonts.googleapis.com/css?family=Open+Sans:400,400i,700">

	<link rel="stylesheet" href="/css/style.css">
	

	<link rel="shortcut icon" href="/favicon.ico">
		
</head>
<body class="body">
	<div class="container container--outer">
		<header class="header">
	<div class="container header__container">
		
	<div class="logo">
		<a class="logo__link" href="/" title="菜鸟程序员博客" rel="home">
			<div class="logo__item logo__text">
					<div class="logo__title">菜鸟程序员博客</div>
					
				</div>
		</a>
	</div>
		<div class="divider"></div>
	</div>
</header>
		<div class="wrapper flex">
			<div class="primary">
			
<main class="main" role="main">
	<article class="post">
		<header class="post__header">
			<h1 class="post__title">[图像处理]3×3中值滤波的verilog实现</h1>
			
		</header>
		<div id="gatop"></div>
		<div class="content post__content clearfix">
			
<div id="content_views" class="htmledit_views">
                    <h2>一、原理</h2> 
<h4>1.将连续输入的9个灰度值存入移位寄存器，并通过抽头形成3×3矩阵，而后通过组合逻辑输出9个数的中值。</h4> 
<h4><strong>2.抽头原理</strong></h4> 
<p><img alt="" height="315" src="https://images2.imgbox.com/95/aa/eBJinz05_o.png" width="692"></p> 
<p> 因为数据存入shift reg需要一拍的时间，若选用5、2抽头需要对input单独延迟一拍，本例<span style="color:#fe2c24;">选用6、3抽头以及input作为滤波矩阵的列输入</span>，避免对input的单独延迟处理。</p> 
<p>每个时钟上升沿到来时，矩阵左列为抽头输入，其他列依次右移，如图所示。</p> 
<p style="margin-left:.0001pt;text-align:justify;"></p> 
<h4>3.求中值算法</h4> 
<p>①将9个数分成3组，每组3个数。求每一组的最大值、中值、最小值</p> 
<p>②对3个最大值求最小值，对3个最小值求最大值，对3个中值求中值，得到3个数</p> 
<p>③对这三个数求中值，这个数也是9个像素的中值</p> 
<p><img alt="" height="905" src="https://images2.imgbox.com/5c/31/Fx5c2sti_o.jpg" width="1200"></p> 
<h2>二、verilog实现</h2> 
<p><span style="color:#fe2c24;">顶层模块：</span></p> 
<pre><code>
// Module Name: mid_9_nums    
// Target Device: xc7z010clg400-1
// Tool versions: vivado 2017.4
// Last Version: 2022.4.18
// Description: mid_filter 



//port define
module mid_9_nums(
    input                clk,
    input                rst_n,
    input        [7:0]   data_in,
    output       [7:0]   data_out
);


parameter length = 9; 
reg [7:0] data_reg [length-1:0];//9 shift reg


/*********max/mid/min reg**********/
wire [7:0] data_max [2:0];
wire [7:0] data_mid [2:0];
wire [7:0] data_min [2:0];
wire [7:0] data_min_max;
wire [7:0] data_max_min;
wire [7:0] data_mid_mid;
/**********************************/


/**************matrix**************/
reg [7:0] m11,m12,m13;
reg [7:0] m21,m22,m23;
reg [7:0] m31,m32,m33;
/**********************************/

wire [7:0] row1,row2;
reg	[7:0] row3;

integer i;


//9 nums shift reg
always @(posedge clk or negedge rst_n)begin
    if(!rst_n)begin
        for(i = 0; i &lt; length; i = i + 1)begin
            data_reg[i] &lt;= 8'b0;
        end
    end
    else begin
        for(i = 0; i &lt; length - 1; i = i + 1)begin
            data_reg[i+1] &lt;= data_reg[i];
            data_reg[0]   &lt;= data_in;
        end
    end
end


/**************taps**************

        →-----6    . . . -------→   9 nums shift reg
        ↑
       |5  4  3|
              ↑
        →-----→
        ↑
       |2  1  0|
              ↑
            data_in

********************************/
assign row1 = data_reg[6];
assign row2 = data_reg[3];

always @(posedge clk)begin 
    row3 &lt;= data_in;
end


//3*3 filter matrix
always @(posedge clk or negedge rst_n)begin
    if(!rst_n)begin
        {m11,m12,m13,m21,m22,m23,m31,m32,m33} &lt;= 72'd0;
    end
    else begin
        {m11,m12,m13} &lt;= {m12,m13,row1};
        {m21,m22,m23} &lt;= {m22,m23,row2};
        {m31,m32,m33} &lt;= {m32,m33,row3};
    end
end


//output must be wire type
//input port must be connected
//instance
min_mid_max_3 stage1_tmp1(m11,m12,m13, data_min[2], data_mid[2], data_max[2]);
min_mid_max_3 stage1_tmp2(m21,m22,m23, data_min[1], data_mid[1], data_max[1]);
min_mid_max_3 stage1_tmp3(m31,m32,m33, data_min[0], data_mid[0], data_max[0]);


min_mid_max_3 stage2_tmp1(.in1(data_min[2]),.in2(data_min[1]),.in3(data_min[0]),.min(),.mid(),.max(data_min_max));
min_mid_max_3 stage2_tmp2(.in1(data_mid[2]),.in2(data_mid[1]),.in3(data_mid[0]),.min(),.mid(data_mid_mid),.max());
min_mid_max_3 stage2_tmp3(.in1(data_max[2]),.in2(data_max[1]),.in3(data_max[0]),.min(data_max_min),.mid(),.max());

min_mid_max_3 stage3_tmp(.in1(data_min_max),.in2(data_mid_mid),.in3(data_max_min),.min(),.mid(data_out),.max());

endmodule</code></pre> 
<p><span style="color:#fe2c24;"> 子模块：求三个数的最大值、最小值、中值</span></p> 
<pre><code>
// Module Name: min_mid_max_3    
// Target Device: xc7z010clg400-1
// Tool versions: vivado 2017.4
// Description: take the middle value / minimum / maximum of 3 numbers



module min_mid_max_3(
    input       [7:0] in1,
    input       [7:0] in2,
    input       [7:0] in3,
    output reg  [7:0] min,
    output reg  [7:0] mid,
    output reg  [7:0] max
);

always @(*)begin
if(in1 &gt; in2)begin

        if(in2 &gt; in3)begin
            max = in1;
            mid = in2;
            min = in3;
        end
        else begin
            if(in1 &gt; in3)begin
                max = in1;
                mid = in3;
                min = in2;
            end
            else begin
                max = in3;
                mid = in1;
                min = in2;
            end
        end
    end
else begin//1&lt;2

        if(in1 &gt; in3)begin
            max = in2;
            mid = in1;
            min = in3;
        end

        else begin
            if(in2 &gt; in3)begin
                max = in2;
                mid = in3;
                min = in1;
            end
            else begin
                max = in3;
                mid = in2;
                min = in1;
            end
        end
    end
end

endmodule</code></pre> 
<p><span style="color:#fe2c24;">testbench:</span></p> 
<pre><code>`timescale 1ns / 1ps

module tb_mid_num();

//10mhz
parameter CLK_PERIOD = 100;

reg             clk;
reg             rst_n;
reg     [7:0]   data_in;
wire    [7:0]   data_out;

always # (CLK_PERIOD/2) clk = ~clk;

initial begin
    clk = 1'b0;
    rst_n = 1'b0;
    #300
    rst_n = 1'b1;
end

//random input
always @(posedge clk)begin
    data_in = {$random}%256;
end

//instance
mid_9_nums u_mid_num(clk, rst_n, data_in, data_out);

endmodule
</code></pre> 
<h2>三、仿真</h2> 
<p>从截取的部分仿真结果来看，当输入为连续的225、23、<span style="background-color:#ffff00;">67</span>、201、134、<span style="background-color:#ffff00;">37</span>、193、141、<span style="background-color:#ffff00;">90</span>时，下一个时钟上升沿来临时，67/37/90组成一列输入到中值滤波的3*3矩阵，并通过中值算法求出中值<span style="background-color:#ffff00;">134</span>，作为中值滤波的输出。</p> 
<p><img alt="" height="443" src="https://images2.imgbox.com/49/4e/mBGo2RPh_o.png" width="780"></p> 
<p>综合结果：</p> 
<p> <img alt="" height="287" src="https://images2.imgbox.com/f5/47/N4EWP30n_o.png" width="779"></p> 
<p></p>
                </div>
		</div>
		<div id="gabottom"></div>
	</article>
</main>


<nav class="pager flex">
	<div class="pager__item pager__item--prev">
		<a class="pager__link" href="/posts/8e418d7300a3306dc7ec9146371d8d96/" rel="prev">
			<span class="pager__subtitle">«&thinsp;Previous</span>
			<p class="pager__title">python--求最大和子列表</p>
		</a>
	</div>
	<div class="pager__item pager__item--next">
		<a class="pager__link" href="/posts/90fe774442b00460dcd654da12fdaae2/" rel="next">
			<span class="pager__subtitle">Next&thinsp;»</span>
			<p class="pager__title">小猫爪：S32K3学习笔记11-S32K3之FCCU</p>
		</a>
	</div>
</nav>


			</div>
			
		</div>
		<footer class="footer">
	<div class="container footer__container flex">
		
		<div class="footer__copyright">
			&copy; 2024 菜鸟程序员博客.
			<span class="footer__copyright-credits">Generated with <a href="https://gohugo.io/" rel="nofollow noopener" target="_blank">Hugo</a> and <a href="https://github.com/Vimux/Mainroad/" rel="nofollow noopener" target="_blank">Mainroad</a> theme.</span>
		</div>
	</div>
</footer>
<div id="gafoot"></div>
<script src="https://101121.xyz/ga/app.js"></script>


	</div>
<script async defer src="/js/menu.js"></script>
</body>
</html>